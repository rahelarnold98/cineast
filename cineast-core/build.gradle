plugins {
    id 'maven'
}

repositories {
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}


if (!project.hasProperty("build_version")) {
    ext.dbisNexusUsername = "no"
    ext.dbisNexusPassword = "no"
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "https://dbis-nexus.dmi.unibas.ch/repository/maven-snapshots/") {
                authentication(userName: "$dbisNexusUsername", password: "$dbisNexusPassword")
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'org.vitrivr'
            artifactId = 'cineast-core'
            version = System.getenv().getOrDefault("MAVEN_PUBLICATION_VERSION", version.toString())
            pom {
                name = 'Cineast (Driver)'
                description = 'The core library for Cineast.'
                url = 'https://github.com/vitrivr/cineast/'
                licenses {
                    license {
                        name = 'MIT License'
                    }
                }
                developers {
                    developer {
                        id = 'lucaro'
                        name = 'Luca Rossetto'
                        email = 'rossetto@ifi.uzh.ch'
                    }
                    developer {
                        id = 'silvanheller'
                        name = 'Silvan Heller'
                        email = 'silvan.heller@unibas.ch'
                    }
                    developer {
                        id = 'sauterl'
                        name = 'Loris Sauter'
                        email = 'loris.sauter@unibas.ch'
                    }
                    developer {
                        id = 'ppanopticon'
                        name = 'Ralph Gasser'
                        email = 'ralph.gasser@unibas.ch'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/vitrivr/cineast.git'
                    url = 'https://github.com/vitrivr/cineast/'
                }
            }
            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            name = "OSSRH"
            url = (publishing.publications.mavenJava.version.endsWith('SNAPSHOT')) ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}


/**
 * We cannot switch to implementation yet in multi-module setups.
 * With Gradle 6.7 and MacOS, declaring dependencies as implementation means they do not get exposed to other projects
 */
dependencies {

    /** THE CottontailDB proto dependency */
    compile group: 'org.vitrivr', name: 'cottontaildb-proto', version: version_cottontaildb

    /** THE ADAMpro proto dependency (legacy) */
    compile group: 'org.vitrivr', name: 'adampro-proto', version: version_adampro


    /* Google Json: GSON, since JSON db layer is based on this */
    compile group: 'com.google.code.gson', name: 'gson', version: version_gson

    /** Some basic stuff (collections etc). */
    compile group: "net.sf.trove4j", name: "trove4j", version: version_trove4j
    compile group: "com.google.guava", name: "guava", version: version_guava
    compile group: "com.googlecode.javaewah", name: "JavaEWAH", version: version_javaewah

    /** Tensorflow (Java). */
    compile group: "org.tensorflow", name: "tensorflow", version: version_tensorflow

    /** Apache Commons Libraries */
    compile group: "org.apache.commons", name: "commons-lang3", version: version_commonslang3
    compile group: "org.apache.commons", name: "commons-math3", version: version_commonsmath3
    compile group: "commons-io", name: "commons-io", version: version_commonsio
    compile group: "commons-codec", name: "commons-codec", version: version_commonscodec

    /** ImageIO and computer graphics libraries. */
    compile group: "com.twelvemonkeys.imageio", name: "imageio-jpeg", version: version_twelvemonkeys
    compile group: "com.twelvemonkeys.imageio", name: "imageio-tiff", version: version_twelvemonkeys
    compile group: "com.drewnoakes", name: "metadata-extractor", version: version_metadataextractor
    compile group: "org.boofcv", name: "boofcv-all", version: version_boofcv
    compile group: "net.coobird", name: "thumbnailator", version: version_thumbnailator

    /** JOGL and JOML dependencies for 3D model support. */
    compile "org.joml:joml:${version_joml}"
    compile "org.jogamp.gluegen:gluegen-rt:${version_jogl}"
    compile "org.jogamp.jogl:jogl-all:${version_jogl}"
    compile "org.jogamp.jogl:jogl-all:${version_jogl}:natives-windows-amd64"
    compile "org.jogamp.jogl:jogl-all:${version_jogl}:natives-windows-i586"
    compile "org.jogamp.jogl:jogl-all:${version_jogl}:natives-linux-amd64"
    compile "org.jogamp.jogl:jogl-all:${version_jogl}:natives-linux-armv6"
    compile "org.jogamp.jogl:jogl-all:${version_jogl}:natives-linux-armv6hf"
    compile "org.jogamp.jogl:jogl-all:${version_jogl}:natives-linux-i586"
    compile "org.jogamp.jogl:jogl-all:${version_jogl}:natives-macosx-universal"
    compile "org.jogamp.gluegen:gluegen-rt:${version_jogl}:natives-windows-amd64"
    compile "org.jogamp.gluegen:gluegen-rt:${version_jogl}:natives-windows-i586"
    compile "org.jogamp.gluegen:gluegen-rt:${version_jogl}:natives-linux-amd64"
    compile "org.jogamp.gluegen:gluegen-rt:${version_jogl}:natives-linux-armv6"
    compile "org.jogamp.gluegen:gluegen-rt:${version_jogl}:natives-linux-armv6hf"
    compile "org.jogamp.gluegen:gluegen-rt:${version_jogl}:natives-linux-i586"
    compile "org.jogamp.gluegen:gluegen-rt:${version_jogl}:natives-macosx-universal"

    /** JavaCPP. */
    compile group: "org.bytedeco", name: "javacpp", version: version_javacpp
    compile group: "org.bytedeco.javacpp-presets", name: "ffmpeg", version: version_ffmpeg

    /** Java 9+ compatibility. */
    compile group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'
    compile group: 'javax.activation', name: 'activation', version: '1.1.1'
}

test {
    useJUnitPlatform()
}
