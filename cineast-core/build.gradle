plugins {
    id "com.google.osdetector" version "1.6.2"
    id "com.google.protobuf" version "0.8.2"
}

project.ext.jomlVersion = "1.9.6"
project.ext.joglVersion = "2.3.2"
project.ext.ffmpegVersion = "4.1-1.4.4"
project.ext.grpcVersion = "1.22.1"
project.ext.protobufVersion = "3.9.0"

def osclassifier = osdetector.classifier
if(osdetector.arch == "x86_32"){
    osclassifier = osdetector.os + "-x86"
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generatedFilesBaseDir = "$projectDir/generated/"
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += "generated/main/java"
            srcDirs += "generated/main/grpc"
        }
        proto {
            //srcDirs += "adampro-grpc/src/main/protobuf"
            srcDirs += "cottontaildb-proto/src/main/protobuf"
        }
    }
}

jar {
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

dependencies {
    compile group: "com.squareup.okhttp3", name: "okhttp", version: "3.10.0"
    compile group: "org.boofcv", name: "boofcv-all", version: "0.29"
    compile group: "org.ejml", name: "ejml-all", version: "0.33"
    compile group: "org.ejml", name: "ejml-core", version: "0.33"
    compile group: "org.jcodec", name: "jcodec-javase", version: "0.1.9"
    compile group: "net.coobird", name: "thumbnailator", version: "0.4.8"
    compile group: "net.sf.trove4j", name: "trove4j", version: "3.0.3"
    compile group: "com.google.guava", name: "guava", version: "24.1-jre"

    compile group: "commons-codec", name: "commons-codec", version: "1.11"
    compile group: "commons-cli", name: "commons-cli", version: "1.4"
    compile group: "com.google.protobuf", name: "protobuf-java", version: "$protobufVersion"
    compile group: "io.grpc", name: "grpc-netty", version: "${grpcVersion}"
    compile group: "io.grpc", name: "grpc-protobuf", version: "${grpcVersion}"
    compile group: "io.grpc", name: "grpc-stub", version: "${grpcVersion}"
    compile group: "org.bytedeco", name: "javacpp", version: "1.3.3"
    compile group: "org.jgrapht", name: "jgrapht-core", version: "1.0.1"
    compile group: "com.sparkjava", name: "spark-core", version: "2.7.1"

    compile group: "com.googlecode.javaewah", name: "JavaEWAH", version: "1.1.6"
    compile group: "org.tensorflow", name: "tensorflow", version: "1.9.0"
    compile group: "com.eclipsesource.minimal-json", name: "minimal-json", version: "0.9.5"

    /** Apache Commons Libraries */
    compile group: "org.apache.commons", name: "commons-lang3", version: "3.9"
    compile group: "org.apache.commons", name: "commons-math3", version: "3.6.1"
    compile group: "org.apache.commons", name: "commons-csv", version: "1.5"
    compile group: "de.svenkubiak", name: "jBCrypt", version: "0.4"
    compile group: "commons-io", name: "commons-io", version: "2.6"


    /** TwelveMonkey"s ImageIO library + Metadata Extractor for advanced image support . */
    compile group: "com.twelvemonkeys.imageio", name: "imageio-jpeg", version: "3.4.1"
    compile group: "com.twelvemonkeys.imageio", name: "imageio-tiff", version: "3.4.1"
    compile group: "com.drewnoakes", name: "metadata-extractor", version: "2.10.1"

    /**
     * JOGL un JOML dependencies for 3D model support.
     */
    compile "org.joml:joml:${jomlVersion}"
    compile "org.jogamp.gluegen:gluegen-rt:${joglVersion}"
    compile "org.jogamp.jogl:jogl-all:${joglVersion}"

    /** Java 9+ */
    compile group: "com.sun.activation", name: "javax.activation", version: "1.2.0"
    compile group: "javax.annotation", name: "javax.annotation-api", version: "1.3.1"

    /** Manual Fixes for the javacpp-devs because of naming conventions & versions. Each os is handled independently. */
    if(osdetector.os == "windows"){

        compile group: "org.bytedeco.javacpp-presets", name: "ffmpeg", version: "${ffmpegVersion}"
        compile group: "org.bytedeco.javacpp-presets", name: "ffmpeg", version: "${ffmpegVersion}", classifier: osclassifier

        compile "org.jogamp.gluegen:gluegen-rt:${joglVersion}:natives-windows-amd64"
        compile "org.jogamp.gluegen:gluegen-rt:${joglVersion}:natives-windows-i586"

        compile "org.jogamp.jogl:jogl-all:${joglVersion}:natives-windows-amd64"
        compile "org.jogamp.jogl:jogl-all:${joglVersion}:natives-windows-i586"
    }
    if(osdetector.os.contains("linux")){

        compile group: "org.bytedeco.javacpp-presets", name: "ffmpeg", version: "${ffmpegVersion}"
        compile group: "org.bytedeco.javacpp-presets", name: "ffmpeg", version: "${ffmpegVersion}", classifier: osclassifier

        compile "org.jogamp.gluegen:gluegen-rt:${joglVersion}:natives-linux-amd64"
        compile "org.jogamp.gluegen:gluegen-rt:${joglVersion}:natives-linux-armv6"
        compile "org.jogamp.gluegen:gluegen-rt:${joglVersion}:natives-linux-armv6hf"
        compile "org.jogamp.gluegen:gluegen-rt:${joglVersion}:natives-linux-i586"

        compile "org.jogamp.jogl:jogl-all:${joglVersion}:natives-linux-amd64"
        compile "org.jogamp.jogl:jogl-all:${joglVersion}:natives-linux-armv6"
        compile "org.jogamp.jogl:jogl-all:${joglVersion}:natives-linux-armv6hf"
        compile "org.jogamp.jogl:jogl-all:${joglVersion}:natives-linux-i586"
    }
    if(osdetector.os == "osx"){

        compile group: "org.bytedeco.javacpp-presets", name: "ffmpeg", version: "${ffmpegVersion}"
        compile group: "org.bytedeco.javacpp-presets", name: "ffmpeg", version: "${ffmpegVersion}", classifier: "mac" + osclassifier

        compile "org.jogamp.gluegen:gluegen-rt:${joglVersion}:natives-macosx-universal"
        compile "org.jogamp.jogl:jogl-all:${joglVersion}:natives-macosx-universal"
    }
}