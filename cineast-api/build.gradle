plugins {
    id 'application'
    id "com.google.protobuf" version "0.8.2"
}


application {
    mainClassName = 'org.vitrivr.cineast.api.Main'
    applicationDefaultJvmArgs = ["-Xms1G", "-Xmx2G"]
}

distributions {
    main {
        contents {
            from('../cineast.json') {
                into 'bin'
            }
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
    }
    generatedFilesBaseDir = "$projectDir/build/generated/"
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += "$projectDir/build/generated/main/grpc"
            srcDirs += "$projectDir/build/generated/main/java"
        }
        proto {
            srcDirs += "cineast-proto/src/main/protobuf"
        }
    }
}

configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

repositories {
    maven {
        url "https://packagecloud.io/manusant/beerRepo/maven2"
    }
    maven {
        // DBIS Nexus // TODO Replace with official, at some point in the future
        url "https://dbis-nexus.dmi.unibas.ch/repository/maven2/"
    }
}

task fatJar(type: Jar) {
    archiveClassifier = 'full'
    manifest {
        attributes 'Main-Class': 'org.vitrivr.cineast.api.Main'
        attributes 'Multi-Release': 'true'
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

dependencies {
    compile group: 'io.javalin', name: 'javalin', version: '3.8.0'
	compile group: 'io.swagger.core.v3', name: 'swagger-core', version: '2.0.6'
	compile group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: '2.10.1'
	compile group: 'org.webjars', name: 'swagger-ui', version: '3.25.0'
    compile group: 'cc.vileda', name: 'kotlin-openapi3-dsl', version: '0.20.2'
    compile group: 'com.squareup.okhttp3', name: 'okhttp', version: '3.10.0'
    compile group: 'de.svenkubiak', name: 'jBCrypt', version: '0.4.1'
}


