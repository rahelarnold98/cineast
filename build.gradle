plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'java'
    id 'idea'
    id 'de.undercouch.download' version "4.1.1"
    id 'com.google.osdetector' version '1.6.2'
}

allprojects {
    /* Group name of our artifacts */
    group = 'org.vitrivr'
    /* Our current version */
    version = '3.0.3'

    apply plugin: 'java'
}

project.ext.protobufVersion = "3.14.0"
project.ext.grpcVersion = "1.34.0"


subprojects {
    buildscript {
        repositories {
            jcenter()
            mavenCentral()
            maven {
                url "https://plugins.gradle.org/m2"
            }
        }

        dependencies {
            classpath "gradle.plugin.com.google.gradle:osdetector-gradle-plugin:1.6.2"
            classpath "gradle.plugin.com.google.protobuf:protobuf-gradle-plugin:0.8.14"
            classpath "de.undercouch:gradle-download-task:4.1.1"
        }
    }

    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
        mavenCentral()

    }

    /* Important warnings that are currently missing simply because there are too many: "cast", "rawtypes". Ideally, "all" should be used in the future. */
    def enabledWarnings = ["-Xlint:deprecation", "-Xlint:empty", "-Xlint:overrides", "-Xlint:serial", "-Xlint:static", "-Xlint:unchecked", "-Xlint:varargs"]
    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs += enabledWarnings
    }
    compileTestJava.options.compilerArgs += enabledWarnings

    dependencies {
        /** Log4j 2 */
        compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.12.0'
        compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.12.0'
        compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.12.0'

        /** Jackson (JSON conversion) */
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.12.0'
        compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.12.0'

        /** Test dependencies (JUnit 5) */
        testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.0'
        testCompile group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.3.2'
        testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.5.0'

        /** Protobuf & gRPC. */
        compile group: "com.google.protobuf", name: "protobuf-java", version: "$protobufVersion"
        compile group: "io.grpc", name: "grpc-netty", version: "${grpcVersion}"
        compile group: "io.grpc", name: "grpc-protobuf", version: "${grpcVersion}"
        compile group: "io.grpc", name: "grpc-stub", version: "${grpcVersion}"
    }
}

project(':cineast-runtime') {
    dependencies {
        compile project(path: ':cineast-core', configuration: 'shadow')
    }
}

project(':cineast-api') {
    dependencies {
        compile project(path: ':cineast-runtime', configuration: 'shadow')
        compile project(path: ':cineast-core')
    }
}

idea {
    module {
        downloadJavadoc = true

        excludeDirs += file('data')
        excludeDirs += file('thumbnails')
    }
}

task getExternalFiles {
    doLast {
        def fileList = new File("externalFiles.csv")
        fileList.eachLine { String line ->
            def split = line.split(",")
            download {
                src split[0]
                dest split[1]
            }
        }
    }
}

task generateOpenApiSpecs(type: JavaExec){
    classpath = project(":cineast-api").sourceSets.main.runtimeClasspath
    main = 'org.vitrivr.cineast.api.docs.GenerateOpenApiSpecs'

    def config = project.hasProperty("cineastConfig") ? project.getProperty("cineastConfig") : "cineast.json"

    args("${config}")
}

